import os

configfile: "config/snakemake_config.yml"

models = config["models"]
variables = config["variables"]
resolutions = config["resolutions"]
firstyear = config["firstyear_inclusive"]
lastyear = config["lastyear_inclusive"]

years = list(range(firstyear, lastyear+1))
months = [f"{m:02d}" for m in range(1, 13)]

regions = config["regions"]

wildcard_constraints:
    regionname="[^/_]+", # ensure regionname does not contain a "/"
    resolution="[^_]+",
    model="[^_]+", # ensure model does not contain a "_"
    variable="[^_]+",
    year=r"\d{4}",  # Ensures the year wildcard matches exactly 4 digits, otherwise it might interfere with {month} right after it.
    month=r"\d{2}",
    firstyear=r"\d{4}",  # Ensures the year wildcard matches exactly 4 digits, otherwise it might interfere with {month} right after it.
    lastyear=r"\d{4}",  # Ensures the year wildcard matches exactly 4 digits, otherwise it might interfere with {month} right after it.

rule all:
    input:
        expand("results/isimip_downloaded_data/{model_upper}/{model}_obsclim_{variable}_{resolution}_global_daily_{year}{month}.nc",
           variable=variables,
           year=years,
           month=months,
           model=models,
           model_upper=[m.upper() for m in models],  # Ensure the uppercase versions are expanded here
           resolution=resolutions,
        ),
        expand("results/isimip_downloaded_data/{model_upper}/{model}_obsclim_{variable}_{resolution}_{regionname}_daily_{year}{month}.nc",
           variable=variables,
           year=years,
           month=months,
           model=models,
           model_upper=[m.upper() for m in models],  # Ensure the uppercase versions are expanded here
           resolution=resolutions,
           regionname=list(regions.keys())
        ),
        expand("results/isimip_downloaded_data/{model_upper}/{model}_obsclim_{variable}_{resolution}_{regionname}_daily_fullyear_{year}.nc",
            variable=variables,
            year=years,
            model=models,
            model_upper=[m.upper() for m in models],  # Ensure the uppercase versions are expanded here
            resolution=resolutions,
            regionname=list(regions.keys())
        ),
        expand("results/isimip_downloaded_data/{model_upper}/{model}_obsclim_{variable}_{resolution}_{regionname}_daily_fullyear_{year}_noleap.nc",
            variable=variables,
            year=years,
            model=models,
            model_upper=[m.upper() for m in models],  # Ensure the uppercase versions are expanded here
            resolution=resolutions,
            regionname=list(regions.keys())
        ),
        expand("results/isimip_prepared_data/{model}/{model}_obsclim_{variable}_{resolution}_{regionname}_daily_allyears.nc",
            variable=variables,
            year=years,
            model=models,
            resolution=resolutions,
            regionname=list(regions.keys())
        ),
        expand("results/isimip_prepared_data/{model}/{model}_obsclim_{variable}_{resolution}_{regionname}_daily_allyears_ncks.nc",
            variable=variables,
            year=years,
            model=models,
            resolution=resolutions,
            regionname=list(regions.keys())
        ),
        expand("results/lpjguess_instruction_files/{model}_{resolution}_{regionname}.ins",
                model=models,
                resolution=resolutions,
                regionname=list(regions.keys())
        ),
        expand("results/lpjguess_instruction_files/{model}_{resolution}_{regionname}_gridlist.txt",
                model=models,
                resolution=resolutions,
                regionname=list(regions.keys()),
        ),
        "results/isimip_prepared_data/co2_obsclim_annual_1850_2021.txt",
        expand("results/lpjguess_outputs_{model}_{resolution}_{regionname}/cpool.out",
            model=models, resolution=resolutions, regionname=list(regions.keys())),
        "output_analysis.html"

rule download_isimip_climate_data:
    output:
        "results/isimip_downloaded_data/{model_upper}/{model}_obsclim_{variable}_{resolution}_global_daily_{year}{month}.nc",
    log: "snakemake_logs/download_isimip_climate_data_{model_upper}_{model}_{variable}_{resolution}_{year}_{month}.log"
    params:
        directory=lambda wildcards: f"results/isimip_downloaded_data/{wildcards.model.upper()}/",
        url=lambda wildcards: f"https://files.isimip.org/ISIMIP3a/InputData/climate/atmosphere/obsclim/global/daily/historical/{wildcards.model.upper()}/{wildcards.model}_obsclim_{wildcards.variable}_{wildcards.resolution}_global_daily_{wildcards.year}{wildcards.month}.nc"
    conda:
        "envs/environment.yml"
    shell:
        """
        mkdir -p {params.directory}
        echo "Downloading {params.url} ..."
        wget -nc -P {params.directory} {params.url}
        """

rule crop_isimip_data:
    # no expansion here, because we want to run the shell command for each input file.
    input:
        "results/isimip_downloaded_data/{model_upper}/{model}_obsclim_{variable}_{resolution}_global_daily_{year}{month}.nc"
    output:
        "results/isimip_downloaded_data/{model_upper}/{model}_obsclim_{variable}_{resolution}_{regionname}_daily_{year}{month}.nc"
    log: "snakemake_logs/crop_isimip_data_{model_upper}_{model}_{variable}_{resolution}_{regionname}_{year}_{month}.log"
    conda:
        "envs/environment.yml"
    params:
       minlon = lambda wildcards: [regions[wildcards.regionname]['minlon']],
       maxlon = lambda wildcards: [regions[wildcards.regionname]['maxlon']],
       minlat = lambda wildcards: [regions[wildcards.regionname]['minlat']],
       maxlat = lambda wildcards: [regions[wildcards.regionname]['maxlat']],
    shell:
        """
        echo "{output}"
        cdo sellonlatbox,{params.minlon},{params.maxlon},{params.minlat},{params.maxlat} {input} {output}
        """

rule concat_to_yearly_cropped_isimip_data:
    input:
        lambda wildcards: expand(
            "results/isimip_downloaded_data/{model_upper}/{model}_obsclim_{variable}_{resolution}_{regionname}_daily_{year}{month}.nc",
            model_upper=wildcards.model.upper(),
            model=wildcards.model,
            variable=wildcards.variable,
            resolution=wildcards.resolution,
            regionname=wildcards.regionname,
            year=wildcards.year,
            month=months # no wildcard here: we want to merge all months!
        )
    output:
        "results/isimip_downloaded_data/{model_upper}/{model}_obsclim_{variable}_{resolution}_{regionname}_daily_fullyear_{year}.nc"
    log: "snakemake_logs/concat_to_yearly_cropped_isimip_data_{model_upper}_{model}_{variable}_{resolution}_{regionname}_{year}.log"
    conda:
        "envs/environment.yml"
    shell:
        """
        cdo -O -z zip_9 -mergetime {input} {output}
        """

rule delete_leapdays:
    input:
        "results/isimip_downloaded_data/{model_upper}/{model}_obsclim_{variable}_{resolution}_{regionname}_daily_fullyear_{year}.nc"
    output:
        "results/isimip_downloaded_data/{model_upper}/{model}_obsclim_{variable}_{resolution}_{regionname}_daily_fullyear_{year}_noleap.nc"
    log: "snakemake_logs/delete_leapdays_{model_upper}_{model}_{variable}_{resolution}_{regionname}_{year}.log"
    conda:
        "envs/environment.yml"
    shell:
        """
        cdo -O -delete,month=2,day=29 {input} {output}
        """

rule combine_to_single_input_file:
    input:
        lambda wildcards: expand(
            "results/isimip_downloaded_data/{model_upper}/{model}_obsclim_{variable}_{resolution}_{regionname}_daily_fullyear_{year}_noleap.nc",
            model_upper=wildcards.model.upper(),
            model=wildcards.model,
            variable=wildcards.variable,
            resolution=wildcards.resolution,
            regionname=wildcards.regionname,
            year=years # no wildcard here: we want to merge all years!
        )
    output:
        "results/isimip_prepared_data/{model}/{model}_obsclim_{variable}_{resolution}_{regionname}_daily_allyears_{firstyear}_{lastyear}.nc",
    log: "snakemake_logs/combine_to_single_input_file_{model}_{variable}_{resolution}_{regionname}_{firstyear}_{lastyear}.log"
    conda:
        "envs/environment.yml"
    shell:
        """
        # merge yearly to total data and apply scale factor using unpack function.
        cdo -O -z zip_9 -unpack -mergetime {input} {output}
        """

rule cycle_data:
    # the sole purpose of this rule is to cycle the climate data. LPJ-GUESS requires 30 years of climate data, but
    # for this example, we do not want to download 30 years worth of data, so we just repeat the one year that was
    # downloaded and prepared in the previous rules.
    input:
        lambda wildcards: expand(
            "results/isimip_prepared_data/{model}/{model}_obsclim_{variable}_{resolution}_{regionname}_daily_allyears_{firstyear}_{lastyear}.nc",
            model_upper=wildcards.model.upper(),
            model=wildcards.model,
            variable=wildcards.variable,
            resolution=wildcards.resolution,
            regionname=wildcards.regionname,
            firstyear=firstyear, # no wildcard here: we only call it once for every combination of the variables other than the years variables
            lastyear=lastyear,
        )
    output:
        "results/isimip_prepared_data/{model}/{model}_obsclim_{variable}_{resolution}_{regionname}_daily_allyears.nc",
    log: "snakemake_logs/cycle_data_{model}_{variable}_{resolution}_{regionname}.log"
    run:
        repeated_inputs = " ".join([f"{input}"] * 31)
        shell(f"cdo -settaxis,1990-01-01,00:00:00,1day -copy {repeated_inputs} {output}")

rule adapt_floating_point_precision:
    input:
        "results/isimip_prepared_data/{model}/{model}_obsclim_{variable}_{resolution}_{regionname}_daily_allyears.nc",
    output:
        "results/isimip_prepared_data/{model}/{model}_obsclim_{variable}_{resolution}_{regionname}_daily_allyears_ncks.nc",
    log: "snakemake_logs/adapt_floating_point_precision_{model}_{variable}_{resolution}_{regionname}.log"
    params:
       rounding_factor = lambda wildcards: 100 if wildcards.resolution == '1800arcsec' else 100000,
    conda:
        "envs/environment.yml"
    shell:
        """
        # Need to round the input coordinates, otherwise LPJ-GUESS will complain about this.
        ncap2 -O -s 'lon=round(lon*{params.rounding_factor})/{params.rounding_factor}; lat=round(lat*{params.rounding_factor})/{params.rounding_factor}' {input} {output}
        """


rule create_gridlist:
    input:
        script="workflow/scripts/create_gridlist_for_region.py",
        climatefile="results/isimip_prepared_data/{model}/{model}_obsclim_tas_{resolution}_{regionname}_daily_allyears_ncks.nc"
    output:
        gridlist="results/lpjguess_instruction_files/{model}_{resolution}_{regionname}_gridlist.txt",
        gridlist_cf="results/lpjguess_instruction_files/{model}_{resolution}_{regionname}_gridlist_cf.txt",
    log: "snakemake_logs/create_gridlist_{model}_{resolution}_{regionname}.log"
    params:
       natural_earth_name= lambda wildcards: [regions[wildcards.regionname]['natural_earth_name']],
       natural_earth_level= lambda wildcards: [regions[wildcards.regionname]['natural_earth_level']],
    conda:
        "envs/environment.yml"
    shell:
        """
        echo "call create gridlist with {input.climatefile} and {wildcards.regionname}"
        python workflow/scripts/create_gridlist_for_region.py {input.climatefile} {wildcards.resolution} {wildcards.regionname} {params.natural_earth_name} {params.natural_earth_level} {output.gridlist} {output.gridlist_cf}
        """

rule download_isimip_co2_data:
    output:
        "results/isimip_prepared_data/co2_obsclim_annual_1850_2021.txt",
    log: "snakemake_logs/download_isimip_co2_data.log"
    conda:
        "envs/environment.yml"
    shell:
        """
        wget -nc -P results/isimip_prepared_data/ https://files.isimip.org/ISIMIP3a/InputData/climate/atmosphere_composition/co2/obsclim/co2_obsclim_annual_1850_2021.txt
        """

rule prepare_model_instruction_files:
    input:
        co2file="results/isimip_prepared_data/co2_obsclim_annual_1850_2021.txt",
        insfile_template="resources/template.ins",
        climatefiles=lambda wildcards: expand("results/isimip_prepared_data/{model}/{model}_obsclim_{variable}_{resolution}_{regionname}_daily_allyears_ncks.nc",
            model=wildcards.model,
            resolution=wildcards.resolution,
            regionname=wildcards.regionname,
            variable=variables
        )
    output:
        "results/lpjguess_instruction_files/{model}_{resolution}_{regionname}.ins"
    log: "snakemake_logs/prepare_model_instruction_files_{model}_{resolution}_{regionname}.log"
    params:
        pwd = os.getcwd(),
        firstyear = firstyear,
        lastyear = lastyear,
    conda:
        "envs/environment.yml"
    shell:
        """
        sed -e 's;<MODEL>;{wildcards.model};g' \
            -e 's;<RESOLUTION>;{wildcards.resolution};g' \
            -e 's;<REGION>;{wildcards.regionname};g' \
            -e 's;<FIRSTYEAR>;{params.firstyear};g' \
            -e 's;<LASTYEAR>;{params.lastyear};g' \
            -e 's;<ISIMIP_DIR>;{params.pwd};g' \
            {input.insfile_template} > {output}
        """

rule run_lpj_guess:
    input:
        insfile="results/lpjguess_instruction_files/{model}_{resolution}_{regionname}.ins",
        gridlist="results/lpjguess_instruction_files/{model}_{resolution}_{regionname}_gridlist.txt",
        #TODO: How can I make sure that all variables are checked here, not only tas?
        climatefiles="results/isimip_prepared_data/{model}/{model}_obsclim_tas_{resolution}_{regionname}_daily_allyears_ncks.nc",
        co2file="results/isimip_prepared_data/co2_obsclim_annual_1850_2021.txt"
    output:
        dir=directory("results/lpjguess_outputs_{model}_{resolution}_{regionname}"),
        cpool_output="results/lpjguess_outputs_{model}_{resolution}_{regionname}/cpool.out"
    log: "snakemake_logs/run_lpj_guess_{model}_{resolution}_{regionname}.log"
    conda:
        "envs/environment.yml"
    shell:
        """
        echo "Run LPJ-GUESS"
        mkdir -p {output.dir}
        sudo docker run \
            --user $(id -u):$(id -g)  \
            --mount type=bind,src=./results,target=/results   \
            --mount type=bind,src=./{output.dir},target=/{output.dir}   \
            kgregor/lpj-guess:latest bash -c "cd /{output.dir} ; /guess_4.1/cmake-build-release/guess -input cf /{input.insfile}"
        """


rule provide_postprocessing_as_html:
    input:
        "workflow/notebooks/output_analysis.ipynb",
        "results/lpjguess_outputs_chelsa-w5e5_1800arcsec_bavaria/cpool.out",
    output:
        # only providing one output file here as an example, one could create one for each model/region etc.
        "output_analysis.html"
    log: "snakemake_logs/provide_postprocessing_as_html.log"
    conda:
        "envs/environment.yml"
    shell:
        """
        jupyter nbconvert --execute --to html --template lab --no-input workflow/notebooks/output_analysis.ipynb --output-dir . --output {output}
        """
